plugins {
  id 'com.diffplug.spotless'
  id 'jacoco'
}

eclipse.classpath.file {
  // instruct eclipse plugin to put everything to module path
  whenMerged {
    entries.findAll { it.kind == 'lib' }.each { it.entryAttributes['module'] = 'true' }
  }
}

dependencies {
  testImplementation project(':fireguard')
  testImplementation "io.github.lambdaprime:id.xfunction:20.0"

  def junitVersion = "5.8.1"
  testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
  testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
  testRuntimeOnly "org.junit.platform:junit-platform-commons:1.7.0"
}


String testDir = "$buildDir/fireguard"
String testSrcDir = "$projectDir/src/test"

// Gradle has no UnZip task so we reuse Copy for that
task myTest(type: Copy) {
    mkdir file(testDir)
    from zipTree("${project.rootDir}/fireguard/release/${releaseFileName}")
    into "$buildDir"
}

jacoco {
  // Java 17 support
  toolVersion = "0.8.7"
}

test {
  useJUnitPlatform()
  finalizedBy jacocoTestReport, jacocoTestCoverageVerification

  testLogging {
    events "started", "failed"
  }
}

jacocoTestReport {
  // generate only HTML report
  reports {
    xml.required = false
    csv.required = false
    html.required = true
  }
  sourceDirectories.from = project(':fireguard').sourceSets.main.java.srcDirs
  classDirectories.from = project(':fireguard').sourceSets.main.java.classesDirectory
}

jacocoTestCoverageVerification {
  sourceDirectories.from = project(':fireguard').sourceSets.main.java.srcDirs
  classDirectories.from = project(':fireguard').sourceSets.main.java.classesDirectory
  afterEvaluate {
    classDirectories.setFrom(files(classDirectories.files.collect {
      fileTree(dir: it, exclude:  [
        // none
      ])
    }))
  }
  violationRules {
    rule {
      enabled = true
      element = 'PACKAGE'
      limit {
        counter = 'LINE'
        value = 'COVEREDRATIO'
        minimum = 0.1
      }
    }
  }
}

jar.dependsOn myTest
compileTestJava.dependsOn myTest
myTest.dependsOn project(':fireguard').createRelease

// run spotless and format code before the build
testClasses.dependsOn spotlessApply
